;; A live handshake against kotobase.net:443 over the public internet.
;;
;;   clojure -Sdeps '{:paths ["src" "test" "resources"]}' -M scripts/live_kotobase.clj [path]
;;   clojure -Sdeps '{:paths ["src" "test" "resources"]}' -M scripts/live_kotobase.clj /llms.txt wrong-pin
;;
;; The peer is authenticated by SPKI pin. The pin below is the one
;; `kotoba-lang/aiueos` already uses for this host; it is a public key digest,
;; not a secret. Pass a second argument to run the NEGATIVE case: the same
;; server with a deliberately wrong pin, which must refuse.
(require '[tls.client :as client] '[tls.codec :as c] '[tls.result :as r]
         '[tls.jdk-provider :as jdk] '[tls.transport.jvm :as tp])

(def host "kotobase.net")
(def pin "50602ad366823fcf5274a7c917baa4fd24b9de4fd15635ff501177c83d05473e")

(let [[path wrong?] *command-line-args*
      path (or path "/llms.txt")
      use-pin (if wrong? (apply str (repeat 64 "a")) pin)
      t (tp/socket-transport host 443 {:timeout-ms 15000})]
  (try
    (let [res (client/handshake jdk/provider t {:server-name host :pin-spki-sha256 use-pin})]
      (cond
        (and wrong? (r/error? res))
        (println "refused, as it must:" (pr-str (r/err res)))

        wrong?
        (do (println "ACCEPTED A WRONG PIN -- THIS IS A BUG") (System/exit 1))

        (r/error? res)
        (do (println "HANDSHAKE FAILED:" (pr-str (r/err res))) (System/exit 1))

        :else
        (let [conn (r/val res)]
          (println "handshake ok")
          (println "  cipher suite      " (:tls/suite conn))
          (println "  CertificateVerify " (:tls/certificate-verify-scheme conn))
          (println "  chain             " (count (:tls/peer-certificates conn)) "certificates, leaf"
                   (count (first (:tls/peer-certificates conn))) "DER bytes")
          (println "  authentication    " (pr-str (:tls/authentication conn)))
          (client/write! conn (c/ascii (str "GET " path " HTTP/1.1\r\nHost: " host
                                            "\r\nConnection: close\r\nUser-Agent: org-ietf-tls/0.1\r\n\r\n")))
          (loop [i 0 acc ""]
            (if (> i 60)
              (println "stopped after 60 records," (count acc) "bytes")
              (let [rd (client/read! conn)]
                (cond
                  (r/error? rd) (println "read error:" (pr-str (r/err rd)) "after" (count acc) "bytes")
                  (:tls/closed (r/val rd))
                  (do (println "peer close_notify after" (count acc) "bytes")
                      (println "--- response ---")
                      (println (subs acc 0 (min 900 (count acc)))))
                  :else (recur (inc i) (str acc (apply str (map char (:tls/content (r/val rd)))))))))) 
          (client/close! conn))))
    (finally ((:close t)))))
